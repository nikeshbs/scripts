@echo off
REM ** NetLogon.bat
REM ** Logon script batch file
REM ** Copyright (c) 2002 Richard L. Mueller
REM ** Hilltop Lab web site - http://www.rlmueller.net
REM ** Version 1.0 - November 19, 2002
REM **
REM ** You have a royalty-free right to use, modify, reproduce, or distribute
REM ** this batch file in any way you find useful, provided that you agree
REM ** that the copyright owner above has no warranty, obligations, or
REM ** liability for such use.

REM ** Synchronize time with server.
REM ** This only works on Win9x clients.
REM ** Normal users on NT, W2k, and XP cannot set the time.
net time \\%SERVER% /set /yes

REM ** Check for 32-bit Windows.
If "%windir%"=="" goto NO_WIN
If exist %windir%\system\kernel32.dll goto WIN_OK
If exist %windir%\system32\kernel32.dll goto WIN_OK

REM ** 16-bit Windows or DOS.
goto NO_WIN

:WIN_OK
REM ** Execute VBScript logon script program.
wscript %0\..\NetLogon.vbs
goto EXIT

:NO_WIN
REM ** VBScript programs not supported.
If exist %0\..\DosLogon.bat call %0\..\DosLogon.bat
goto EXIT

:EXIT
